
( system calls )

: emit    0 sys ;
: .       1 sys ;
: tell    2 sys ;
: quit    128 sys ;
: sin     129 sys ;
: include 130 sys ;
: save    131 sys ;

( custom system calls )

: dtell   ( reg -- )					132 sys ;
( when b32in is called, next number pushed is interpreted as base32 and puts addr on stack )
: b32in   ( reg -- )					133 sys ;
: b32tell ( reg -- )					134 sys ;
: init    ( -- )					135 sys ;
: getrand ( random -- )					136 sys ;
: counter ( -- curval )					137 sys ;
: cntrinc ( -- curval )					138 sys ;
: sign    ( digest prislot sig -- )			139 sys ;
: verify  ( digest pubslot sig -- pass)			140 sys ;
: getpub  ( slot pubkey -- )				141 sys ;
: setpub  ( slot pubkey -- )				142 sys ;
: ecdh    ( pubkey prislot sharsec -- )			143 sys ;
: shstat  ( -- status )					144 sys ;
: getser  ( serial -- )					145 sys ;
: rotprep ( seed randout pubkey -- slotconf keyconf)	146 sys ;
: rotate  ( valid verdata gendata sig -- ver)		147 sys ;
: readpub  ( slot pubkey -- )				148 sys ;

: genkey  ( prikey pubkey -- ) 158 sys ;


: sha256i  ( -- ) 168 sys ;
: sha256u  ( buffer -- ) 169 sys ;
: sha256f  ( digest -- ) 170 sys ;
: srotpre ( valid seed randout pubkey serial slotconf keyconf gendata verdata digest -- ) 173 sys ;

: symkeygen ( key -- ) 178 sys ;
: hydroe ( c mlen m msgid key -- ) 179 sys ;
: hydrod ( c m msgid key -- t=mlen f=0 ) 180 sys ;
: memzero ( buffer -- ) 181 sys ;
: rfill ( buffer -- ) 182 sys ;
: hydrokdf ( subkey subkeyid masterkey -- ) 183 sys ;

: memcpy ( dest src -- ) 184 sys ;



( dictionary access. These are shortcuts through the primitive operations are !!, @@ and ,, )

: !    0 !! ;
: @    0 @@ ;
: ,    0 ,, ;
: #    0 ## ;

( typed dictionary access for single byte operations )
: !u8 2 !! ;
: @u8 2 @@ ;
: ,u8 2 ,, ;


( compiler state )

: [ 0 compiling ! ; immediate
: ] 1 compiling ! ;
: postpone 1 _postpone ! ; immediate


( some operators and shortcuts )

: over 1 pick ;
: +!   dup @ rot + swap ! ;
: inc  1 swap +! ;
: dec  -1 swap +! ;
: <    - <0 ;
: >    swap < ;
: <=   over over >r >r < r> r> = + ;
: >=   swap <= ;
: =0   0 = ;
: not  =0 ;
: !=   = not ;
: cr   10 emit ;
: ..   dup . ;
: here h @ ;


( memory management )

: allot  h +!  ;
: var : postpone [ ' lit , here dup 5 + , ' exit , here swap ! 5 allot ;


( 'begin' gets the current address, a jump or conditional jump back is generated
  by 'again', 'until' or 'times' )

: begin   here ; immediate
: again   ' jmp , , ; immediate
: until   ' jmp0 , , ; immediate
: times ' 1 - , ' dup , ' =0 , postpone until ; immediate


( 'if' prepares conditional jump, address will be filled in by 'else' or 'fi' )

: if      ' jmp0 , here 999 , ; immediate
: unless  ' not , postpone if ; immediate
: else    ' jmp , here 999 , swap here swap ! ; immediate
: fi      here swap ! ; immediate


( forth style 'do' and 'loop', including loop iterators 'i' and 'j' )

: i ' lit , 0 , ' pickr , ; immediate
: j ' lit , 2 , ' pickr , ; immediate
: do ' swap , ' >r , ' >r , here ; immediate
: loop+ ' r> , ' + , ' dup , ' >r , ' lit , 1 , ' pickr , ' > , ' jmp0 , , ' r> , ' drop , ' r> , ' drop , ; immediate
: loop ' lit , 1 , postpone loop+ ;  immediate


( Create string literal, puts length and address on the stack )

: s" compiling @ if ' lits , here 0 , fi here begin key dup 34 = if drop
     compiling @ if here swap - swap ! else dup here swap - fi exit else , fi
     again ; immediate

( Print string literal )

: ." compiling @ if postpone s" ' tell , else begin key dup 34 = if drop exit else emit fi again
     fi ; immediate

( crypto registers for big numbers )

var pubkey 64 1 + allot
var sig 64 1 + allot
var random 32 1 + allot
var digest 32 1 + allot
var prikey 32 1 + allot

 var sharesec 32 1 + allot
 var uplink 32 1 + allot
 var dolink 32 1 + allot
 
var serial 9 1 + allot
var verdata 19 1 + allot
var gendata 3 1 + allot
var seed 20 1 + allot

var c 64 1 + allot
var m 28 1 + allot
var k 32 1 + allot


( setting size byte )
64 pubkey !u8
64 sig !u8
32 random !u8
32 digest !u8
32 prikey !u8
 32 sharesec !u8
 32 uplink !u8
 32 dolink !u8
9 serial !u8
19 verdata !u8
3 gendata !u8
20 seed !u8

 64 c !u8
 28 m !u8
 32 k !u8

( methods for handling the dictionary )

( 'next' increases the given dictionary address by the size of the cell
  located at that address )

: next dup # + ;

( 'words' generates a list of all define words )

: name dup @ 31 & swap next dup next rot tell @ ;
: words latest @ begin name 32 emit dup 0 = until cr drop ;

var upmsgid
var domsgid

: ecdhkdf uplink 0 sharesec hydrokdf dolink 1 sharesec hydrokdf 0 upmsgid ! 0 domsgid ! ;



: encrypt c swap m upmsgid @ k hydroe upmsgid dup @ 1 + swap ! ;
: decrypt c m domsgid @ k hydrod dup 0 = unless domsgid dup @ 1 + swap ! fi ;


." Welcome to strongForth, " here . ." bytes used" cr ;


(
vi: ts=3 sw=3 ft=forth
)


